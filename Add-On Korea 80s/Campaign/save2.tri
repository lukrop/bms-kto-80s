//
// Base event file
//
// :::::::::::::::::::::::::::::::::::::::::;
//
// #TOTAL_EVENTS is a required entry, and
// should be # of events we have triggers for
// and code fore 
// KCK WARNING: KEEP THIS UPDATED!
//
#TOTAL_EVENTS 22
//
// :::::::::::::::::::::::::::::::::::::::::;
// 
// #SET_EVENT will set the activated flag for any
// events we want to be initially activate
// ie: Seoul already should be marked as
// captured in the Pusan parameter scenario
//
// This marks Seoul as already taken/attacked
#SET_EVENT 1
#SET_EVENT 2
// This marks the ROK as initially on the offensive
#SET_EVENT 9
// This marks the DPRK as having advanced past the DMZ
#SET_EVENT 5
#SET_EVENT 8
// This sets the initial tempo
#SET_TEMPO 255
#ENDINIT
//
// :::::::::::::::::::::::::::::::::::::::::;
// 
// Now come the individual triggers
// 
// Event #1
// Seoul being attacked by DPRK forces
// 
#IF_EVENT_PLAYED 1
#ELSE
#IF_CONTROLLED 6 O 226 227 228 295
#PLAY_MOVIE 100
#SET_PAK_PRIORITY 6 228 100
#SET_PAK_PRIORITY 2 228 100
#CHANGE_PRIORITIES 6 2
#CHANGE_PRIORITIES 2 1
#SET_TEMPO 255
#DO_EVENT 1
#ENDIF
#ENDIF
//
// Event #2
// Seoul falls/regained
// 
#IF_EVENT_PLAYED 2
#IF_CONTROLLED 2 A 228
#SHIFT_INITIATIVE 6 2 50
#RESET_EVENT 2
#ENDIF
#ELSE
#IF_CONTROLLED 6 A 228
#PLAY_MOVIE 101
#DO_EVENT 2
#SHIFT_INITIATIVE 2 6 50
#ENDIF
#ENDIF
// 
// Event #3
// P'yongyang being attacked by ROK forces
// 
#IF_EVENT_PLAYED 3
#ELSE
#IF_CONTROLLED 2 O 259 260 675 1071
#PLAY_MOVIE 102
#SET_PAK_PRIORITY 6 260 100
#SET_PAK_PRIORITY 2 260 100
#CHANGE_PRIORITIES 6 1
#CHANGE_PRIORITIES 2 2
#SET_TEMPO 255
#DO_EVENT 3
#ENDIF
#ENDIF
// 
// Event #4
// P'yongyang falls/regained
// 
#IF_EVENT_PLAYED 4
#IF_CONTROLLED 6 A 260 
#SHIFT_INITIATIVE 2 6 50
#RESET_EVENT 4
#ENDIF
#ELSE
#IF_CONTROLLED 2 A 260
#PLAY_MOVIE 103
#DO_EVENT 4
#SHIFT_INITIATIVE 6 2 50
#ENDIF
#ENDIF
//
// Event #5
// DPRK advanced beyond DMZ
//
#IF_EVENT_PLAYED 5
#ELSE
#IF_EVENT_PLAYED 7
#ELSE
#IF_CONTROLLED 6 O 126 1836 1833 1826 339 340 794
#DO_EVENT 5
#RESET_EVENT 6
#ENDIF
#ENDIF
#ENDIF
// 
// Event #6
// DPRK pushed back to DMZ
// 
#IF_EVENT_PLAYED 5
#IF_EVENT_PLAYED 7
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#ELSE
#IF_CONTROLLED 2 A 729 730 3117 3144 3145 
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#ENDIF
#ENDIF
#ENDIF
// 
// Event #7
// Combined forces advanced beyond DMZ
//
#IF_EVENT_PLAYED 7
#ELSE
#IF_EVENT_PLAYED 5
#ELSE
#IF_CONTROLLED 2 O 1204 1206 1832 1831 1798 1030 2896 3622
#DO_EVENT 7
#RESET_EVENT 8
#ENDIF
#ENDIF
#ENDIF
// 
// Event #8
// Combined forces pushed back to DMZ
// 
#IF_EVENT_PLAYED 7
#IF_EVENT_PLAYED 5
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#ELSE
#IF_CONTROLLED 6 A 410 2694 3171 2695 1950 108 344 2242 266
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#ENDIF
#ENDIF
#ENDIF
// 
// Event #9
// Combined forces go on offensive
//
#IF_EVENT_PLAYED 10
#IF_ON_OFFENSIVE 2
#PLAY_MOVIE 106
#DO_EVENT 9
#RESET_EVENT 10
#ENDIF
#ENDIF
// 
// Event #10
// Combined forces go on defensive
// 
#IF_EVENT_PLAYED 9
#IF_ON_OFFENSIVE 6
#PLAY_MOVIE 107
#DO_EVENT 10
#RESET_EVENT 9
#ENDIF
#ENDIF
// 
// Event #11
// China joins the war
//
#IF_EVENT_PLAYED 11
#ELSE
#IF_FORCE_RATIO A 6 2 L 6
#PLAY_MOVIE 108
#CHANGE_RELATIONS 5 6 1
#DO_EVENT 11
#ENDIF
#ENDIF
// 
// Event #12
// Russia joins the war
//
#IF_EVENT_PLAYED 12
#ELSE
#IF_FORCE_RATIO A 6 2 L 3
#PLAY_MOVIE 109
#CHANGE_RELATIONS 4 6 1
#DO_EVENT 12
#ENDIF
#ENDIF
// 
// Event #13
// Bad guys win
// Basically, did Seoul and Pusan fall
// 
#IF_CONTROLLED 6 A 228 417
#PLAY_MOVIE 113
#DO_EVENT 13
#END_GAME 13
#ENDIF
// 
// Event #14
// Stalemate
// 
#IF_BORDOM_HOURS 48
#PLAY_MOVIE 114
#DO_EVENT 14
#END_GAME 14
#ENDIF
// 
// Event #15
// Timeout
// 
#IF_CAMPAIGN_DAY G 5
#PLAY_MOVIE 115
#DO_EVENT 15
#END_GAME 15
#ENDIF
// 
// Event #16
// We win - hu-rah!
// Basically - We own Seoul
//
#IF_CONTROLLED 2 A 228
#PLAY_MOVIE 116
#DO_EVENT 16
#END_GAME 16
#ENDIF
// End
#ENDSCRIPT
