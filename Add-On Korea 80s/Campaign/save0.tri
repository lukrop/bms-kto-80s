//
// Base event file
//
// :::::::::::::::::::::::::::::::::::::::::;
//
// #TOTAL_EVENTS is a required entry, and
// should be # of events we have triggers for
// and code fore 
// KCK WARNING: KEEP THIS UPDATED!
//
#TOTAL_EVENTS 22
//
// :::::::::::::::::::::::::::::::::::::::::;
// 
// #SET_EVENT will set the activated flag for any
// events we want to be initially activate
// ie: Seoul already should be marked as
// captured in the Pusan parameter scenario
//
// This marks the ROK as initially on the defensive
#SET_EVENT 10
// This marks the ROK as having advanced beyond the DMZ
#SET_EVENT 6
#SET_EVENT 7
// This sets the initial tempo
#SET_TEMPO 150
#ENDINIT
//
// :::::::::::::::::::::::::::::::::::::::::;
// 
// Now come the individual triggers
// 
// Event #1
// Wonsan being attacked by DPRK forces
// 
#IF_EVENT_PLAYED 1
#ELSE
#IF_CONTROLLED 6 O 1426 1190 1192
#PLAY_MOVIE 107
#SET_PAK_PRIORITY 6 404 100
#SET_PAK_PRIORITY 2 404 100
#SET_PAK_PRIORITY 6 260 95
#SET_PAK_PRIORITY 2 260 95
#SET_TEMPO 255
#DO_EVENT 1
#ENDIF
#ENDIF
//
// Event #2
// Wonsan falls/regained
// 
#IF_EVENT_PLAYED 2
#IF_CONTROLLED 2 A 404
#PLAY_MOVIE 106
#SET_PAK_PRIORITY 6 260 100
#SET_PAK_PRIORITY 2 260 100
#SET_PAK_PRIORITY 6 404 95
#SET_PAK_PRIORITY 2 404 95
#SHIFT_INITIATIVE 2 6 50
#RESET_EVENT 2
#ENDIF
#ELSE
#IF_CONTROLLED 6 A 404
#PLAY_MOVIE 107
#SET_PAK_PRIORITY 6 260 100
#SET_PAK_PRIORITY 2 404 100
#SET_PAK_PRIORITY 6 404 95
#SET_PAK_PRIORITY 2 260 95
#DO_EVENT 2
#SHIFT_INITIATIVE 6 2 50
#ENDIF
#ENDIF
// 
// Event #3
// P'yongyang being attacked by ROK forces
// 
#IF_EVENT_PLAYED 3
#ELSE
#IF_CONTROLLED 6 O 3141 3036 1056 682
#PLAY_MOVIE 102
#SET_PAK_PRIORITY 6 260 100
#SET_PAK_PRIORITY 2 260 100
#SET_PAK_PRIORITY 6 404 95
#SET_PAK_PRIORITY 2 404 95
#SET_TEMPO 255
#DO_EVENT 3
#ENDIF
#ENDIF
// 
// Event #4
// P'yongyang falls/regained
// 
#IF_EVENT_PLAYED 4
#IF_CONTROLLED 2 A 260
#PLAY_MOVIE 103
#SHIFT_INITIATIVE 6 2 50
#RESET_EVENT 4
#ENDIF
#ELSE
#IF_CONTROLLED 6 A 260
#SET_PAK_PRIORITY 6 404 100
#SET_PAK_PRIORITY 2 260 100
#SET_PAK_PRIORITY 6 260 95
#SET_PAK_PRIORITY 2 404 95
#DO_EVENT 4
#SHIFT_INITIATIVE 2 6 50
#ENDIF
#ENDIF
//
// Event #5
// DPRK advanced beyond DMZ
//
#IF_EVENT_PLAYED 5
#ELSE
#IF_EVENT_PLAYED 7
#ELSE
#IF_CONTROLLED 6 O 260 404
#DO_EVENT 5
#RESET_EVENT 6
#ENDIF
#ENDIF
#ENDIF
// 
// Event #6
// DPRK pushed back to DMZ
// 
#IF_EVENT_PLAYED 5
#IF_EVENT_PLAYED 7
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#ELSE
#IF_CONTROLLED 2 A 260 404
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#ENDIF
#ENDIF
#ENDIF
// 
// Event #7
// Combined forces advanced beyond DMZ
//
#IF_EVENT_PLAYED 7
#ELSE
#IF_EVENT_PLAYED 5
#ELSE
#IF_CONTROLLED 2 A 399
#DO_EVENT 7
#RESET_EVENT 8
#ENDIF
#ENDIF
#ENDIF
// 
// Event #8
// Combined forces pushed back to DMZ
// 
#IF_EVENT_PLAYED 7
#IF_EVENT_PLAYED 5
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#ELSE
#IF_CONTROLLED 6 O 260 404
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#ENDIF
#ENDIF
#ENDIF
// 
// Event #9
// Combined forces go on offensive
// 
#IF_EVENT_PLAYED 10
#IF_ON_OFFENSIVE 2
#PLAY_MOVIE 106
#DO_EVENT 9
#SHIFT_INITIATIVE 6 2 100
#RESET_EVENT 10
#ENDIF
#ENDIF
// 
// Event #10
// Combined forces go on defensive
// Triggered by DPRK on offensive and
// capturing P'yongyang or Wonsan
// 
#IF_EVENT_PLAYED 9
#IF_ON_OFFENSIVE 6
#IF_CONTROLLED 6 O 260 404
#PLAY_MOVIE 107
#DO_EVENT 10
#SHIFT_INITIATIVE 2 6 50
#RESET_EVENT 9
#ENDIF
#ENDIF
#ENDIF
// 
// Event #11
// China joins the war
//
#IF_EVENT_PLAYED 11
#ELSE
#IF_MAIN_TARGET 6 260
#PLAY_MOVIE 108
#CHANGE_RELATIONS 5 6 1
#DO_EVENT 11
#ENDIF
#ENDIF
// 
// Event #12
// Russia joins the war
//
#IF_EVENT_PLAYED 12
#ELSE
#IF_CONTROLLED 2 A 399
#PLAY_MOVIE 109
#CHANGE_RELATIONS 4 6 1
#DO_EVENT 12
#ELSE
#IF_CAMPAIGN_DAY G 3
#PLAY_MOVIE 109
#CHANGE_RELATIONS 4 6 1
#DO_EVENT 12
#ENDIF
#ENDIF
#ENDIF
// 
// Event #13
// Bad guys win
// Basically, did P'yongyang and Wonsan fall or does
// DPRK have either city and a 1:1 ratio of forces
// 
#IF_CONTROLLED 6 A 260 404
#PLAY_MOVIE 113
#DO_EVENT 13
#END_GAME 13
#ELSE
#IF_CONTROLLED 6 O 260 404
#IF_FORCE_RATIO G 2 6 L 10
#PLAY_MOVIE 113
#DO_EVENT 13
#END_GAME 13
#ENDIF
#ENDIF
#ENDIF
// 
// Event #14
// Stalemate
// 
#IF_BORDOM_HOURS 96
#PLAY_MOVIE 114
#DO_EVENT 14
#END_GAME 14
#ENDIF
// 
// Event #15
// Timeout
// 
#IF_CAMPAIGN_DAY G 5
#PLAY_MOVIE 115
#DO_EVENT 15
#END_GAME 15
#ENDIF
// 
// Event #16
// (1) We win - hu-rah! Basically - We own Wonsan and P'yongyang
// (1) Along with Onch'ang-ni, Hamju, Huich'on, Kaech'on, Taehung
//
// (2) Alternative win - hu-rah!
// (2) If allies control both Wonsan and P'yongyang
// (2) If allies control any of Onch'ang-ni, Hamju, Huicho'on, Kaech'on, or Taehung
// (2) If DPRK Ground Forces are less than 0.1 of Allied Ground Forces
//
// (3) Alternative win - hu-rah!
// (3) If allies control either of P'yongyang or Wonsan
// (3) If DPRK Ground forces are less than 0.1 of Allied Ground Forces
// (3) Basically attritted DPRK while retaining P'yongyang or Wonsan
//
#IF_CONTROLLED 2 A 404 260 1792 1013 358 454 1698
#PLAY_MOVIE 116
#DO_EVENT 16
#END_GAME 16
#ELSE
#IF_CONTROLLED 2 A 260 404
#IF_CONTROLLED 2 O 1792 1013 358 454 1698
#IF_FORCE_RATIO G 6 2 L 2
#PLAY_MOVIE 116
#DO_EVENT 16
#END_GAME 16
#ENDIF
#ENDIF
#ELSE
#IF_CONTROLLED 2 O 260 404
#IF_FORCE_RATIO G 6 2 L 1
#PLAY_MOVIE 116
#DO_EVENT 16
#END_GAME 16
#ENDIF
#ENDIF
#ENDIF
#ENDIF
// End
#ENDSCRIPT

